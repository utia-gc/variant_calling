nextflow_workflow {

    name "Test Workflow PREPARE_INPUTS"
    script "workflows/prepare_inputs.nf"
    workflow "PREPARE_INPUTS"

    test("PREPARE_INPUTS succeeds when valid samplesheet, genome, and annotation are supplied.") {
        // instantiate helper objects
        def refs = new RefsEnsembl()

        when {
            params {
                publishDirReports = "${outputDir}/reports"
                publishDirData    = "${outputDir}/data"
            }
            workflow {
                """
                input[0] = file("https://github.com/utia-gc/ngs-test/raw/ngs/samplesheets/samplesheet_mix.csv")
                input[1] = file("${refs.getGenomeGzip()}")
                input[2] = file("${refs.getGTFGzip()}")
                """
            }
        }

        then {
            // test workflow status
            assert workflow.success
            assert workflow.trace.succeeded().size() == 4

            // test samplesheet parsing
            assert workflow.out.samples.size() == 4

            // test reference genome prep
            assert workflow.out.genome
            with(workflow.out.genome) {
                assert size() == 1
                assert get(0) ==~ /^.*\/genome_I.fa/
            }

            // test genome fasta indexed
            assert workflow.out.genome_index
            with(workflow.out.genome_index) {
                assert size() == 1
                assert get(0) ==~ /^.*\/genome_I.fa.fai/
            }

            // test sequence dictionary file produced
            with(workflow.out.genome_dict) {
                assert size() == 1
                assert get(0) ==~ /^.*\/genome_I\.dict/
            }

            // test reference annotations prep
            assert workflow.out.annotations
            with(workflow.out.annotations) {
                assert size() == 1
                assert get(0) ==~ /^.*\/annotations_I.gtf/
            }

            // test workflow stability
            assert snapshot(
                workflow.out.samples,
                workflow.out.genome,
                workflow.out.genome_index,
                workflow.out.annotations,
            ).match()
        }

    }

}
